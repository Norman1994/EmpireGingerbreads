label palace:

    scene mansion with dissolve

    "Дворец Зареченского распологался за городом, и идти было до него всего ничего. Я даже не устал."

    "Когда я оказался на месте, на улице царил глубокий прохладный вечер."

    "Дорогу перед домом занимали кареты с лошадьми; их было не больше десятка. Кучеры в ожидании своих господ сидели небольшой группкой у фонтана и весело общались между собой."

    "От вида самого дворца у меня перехватило дыхание."

    "Он возвышался на холме, словно замок из детских сказок."

    "Его бежевый фасад был украшен изысканной лепниной и величественными колонами, а на просторном полукруглом крыльце можно было уместить целую футбольную команду."
    
    "С больших, украшенных бархатными занавесками окон исходил теплый приятный свет, в то время, как всё остальное было сокрыто холодным мраком."

    "О размерах сада, раскинувшегося вокруг, мне даже не хотелось думать. Я мог лишь представлять, как за стеной густых величественных дубов скрывался темный непроходимый лес."

    show conductor with dissolve

    "Старый знакомый проводник поезда возник передо мной, как по волшебству."

    "Откуда он здесь?!"

    conductor "Приветствую, Сударь. Вас приглашали?"

    "На этот раз он смотрел на меня спокойно, без привычной ему раздражительности. Не узнал меня что ли?"

    me "Да, вроде того..."

    conductor "Тогда проследуйте через двери прямо в Главный Зал!"

    hide conductor with dissolve

    "Я был уверен, что этот дядя наверняка обо всем знал, но мне почему-то не хотелось его допытывать."

    "Я вошел внутрь дворца."

    jump grand_hall

    return

label grand_hall:

    scene grand_hall with dissolve

    "Только я вошел в зал, как в глаза мне ударил яркий пронзительный свет."

    "Десятки... Нет, сотни свечей в канделябрях висели на стенах, будто своей яркостью они стремились затмить само солнце."

    "По воздуху плавала медленная, утонченная мелодия, и люди, одетые в бархатные платья и солидные фраки медленно кружились по пространству. Их лица были предельно сосредоточенными."

    "Царила по-настоящему царская, галантная атмосфера праздника, гораздо далекая от наших современных 'тусняков' и 'вечеринок'."

    "Мне даже стало жаль, что эпоха Империи в скором времени исчезнет и уступит место хаосу пролетарской революции."

    show GrafPetr with dissolve

    if varvara_scores > 0:

        petr "Молодой человек, вы явились!"

        petr "Я только и делаю, что целый день рассказываю окружающим о вашем подвиге! Некоторые из моих друзей даже хотят с вами познакомиться!"

        me "Ого... Приятно слышать, граф Зареченский!"

        petr "Зови меня Петр Иванович!"

        petr "Тут где-то была моя дочь, изволите ли вы потанцевать с ней?"

        menu:
            "Согласиться":

                $varvara_scores += 2;

                me "Эм... Ладно!"

                "Граф засиял так, словно я своим согласием спас ему жизнь."

                petr "По поводу дочери смею Вас упросить: будьте с ней вежливы, она немного... встревожена!"

                me "Д-Да... Конечно, буду!"

                petr "Сердечно благодарю вас, молодой человек! Развлекайтесь!"

                hide GrafPetr with dissolve

                "Боже, на что я согласился?! Я же в жизни не танцевал!"

                "Даже Рита накануне нашей свадьбы наняла мне учителя, чтобы я во время торжества не наступал ей на ноги."

                "Какой же я идиот!"

            "Отказаться":

                me "Эм... Простите, Граф, но я не умею танцевать, да и вообще не хочу позориться. Извините."

                petr "Хм... Как изволите, молодой человек, но если передумаете, дайте мне знать! Отдыхайте!"

                hide GrafPetr with dissolve 

                "Ох... А может, стоило согласиться? Ладно, чего уж жалеть."

    else:

        petr "Молодой человек, вы все таки решились посетить мой балл? Неожиданно!"

    return